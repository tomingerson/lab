buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'idea'

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
    mavenCentral()
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

// declare defaults for all subprojects
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {

        compile 'org.apache.commons:commons-lang3:3.+'

        testCompile('junit:junit:4.+') {
            exclude group: 'org.hamcrest'
        }
        testCompile 'org.hamcrest:hamcrest-all:1.+'
    }

    group 'de.fh_kiel'
    version '1.0'
}

// the portal needs the annotation processor and some more dependencies to test
project(':developer_portal') {

    apply plugin: 'org.springframework.boot'

    dependencies {
        def springBootVersion = "1.4.2.RELEASE"

        compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}"){
            exclude module: "logback-classic"
        }
        compile("org.springframework.boot:spring-boot-starter-aop:${springBootVersion}"){
            exclude module: "logback-classic"
        }
        compile 'com.fasterxml.jackson.core:jackson-core:2.8.+'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.8.+'
        compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.+'
        // Jackson cannot deserialize java.time.LocalDate without this lib yet
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.+'


        compileOnly "org.projectlombok:lombok:1.16.+"


        testCompile('org.mockito:mockito-core:1.10.+') {
            exclude group: 'org.hamcrest'
        }
        testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}"){
            exclude module: "logback-classic"
        }
    }
}
